unit Example.Config;

interface

type
  TExampleConfig = record
    ApiUrl: string;
    ApiKey: string;
    Serial: string;
    Token: string;
    MacAddress: string;
    ComputerName: string;
    Email: string;
    Password: string;
    SoftwareId: string;
    SoftwareVersion: string;
    InstalacaoId: string;
    OfflineSecret: string;
    AlertDays: Integer;
    RenewUrl: string;
    class function Default: TExampleConfig; static;
  end;

implementation

uses
  Winapi.Windows, System.SysUtils, System.Hash, System.Win.Registry,
  Winapi.IpHlpApi, Winapi.IpTypes, Winapi.WinSock;

function _GetVolumeInformationW(lpRootPathName: PWideChar; lpVolumeNameBuffer: PWideChar;
  nVolumeNameSize: DWORD; lpVolumeSerialNumber: PDWORD; lpMaximumComponentLength: PDWORD;
  lpFileSystemFlags: PDWORD; lpFileSystemNameBuffer: PWideChar; nFileSystemNameSize: DWORD): BOOL; stdcall;
  external kernel32 name 'GetVolumeInformationW';

const
  MIB_IF_TYPE_LOOPBACK = 24;

function DetectPrimaryMacAddress: string; forward;
function BuildInstallationId(const ComputerName: string): string; forward;

class function TExampleConfig.Default: TExampleConfig;
var
  Buffer: array [0..MAX_COMPUTERNAME_LENGTH] of Char;
  Size: DWORD;
  Guid: TGUID;
begin
  Result.ApiUrl := 'https://express.adassoft.com/api_validacao.php';
  Result.ApiKey := 'SK-CEF4-44DC-91F1-19A7-B2D2-6A58-1876-2004';
  Result.Serial := '';
  Result.Token := '';
  Result.MacAddress := DetectPrimaryMacAddress;
  Result.Email := 'cliente@exemplo.com';
  Result.Password := '';
  Result.SoftwareId := '3';
  Result.SoftwareVersion := '3.10.14';
  Result.OfflineSecret := 'client-offline-secret-demo';
  Result.AlertDays := 10;
  Result.RenewUrl := 'https://express.adassoft.com/licencas/renovar';

  Size := MAX_COMPUTERNAME_LENGTH + 1;
  if GetComputerName(Buffer, Size) then
    SetString(Result.ComputerName, Buffer, Size)
  else
    Result.ComputerName := '';

  Result.InstalacaoId := BuildInstallationId(Result.ComputerName);
  if Result.InstalacaoId = '' then
  begin
    if CreateGuid(Guid) = S_OK then
      Result.InstalacaoId := GuidToString(Guid)
    else
      Result.InstalacaoId := 'INSTALACAO-DEMO-001';
  end;
end;

function DetectPrimaryMacAddress: string;
var
  Buffer: PIP_ADAPTER_INFO;
  OutLen: ULONG;
  Adapter: PIP_ADAPTER_INFO;
  Candidate: string;
begin
  Result := '';
  OutLen := 0;
  if GetAdaptersInfo(nil, OutLen) <> ERROR_BUFFER_OVERFLOW then
    Exit;

  GetMem(Buffer, OutLen);
  try
    if GetAdaptersInfo(Buffer, OutLen) <> ERROR_SUCCESS then
      Exit;

    Adapter := Buffer;
    while Adapter <> nil do
    begin
      if Adapter^.AddressLength >= 6 then
      begin
        Candidate := Format('%.2x:%.2x:%.2x:%.2x:%.2x:%.2x',
          [Adapter^.Address[0], Adapter^.Address[1], Adapter^.Address[2],
           Adapter^.Address[3], Adapter^.Address[4], Adapter^.Address[5]]);
        Result := UpperCase(Candidate);
        if Adapter^.Type_ <> MIB_IF_TYPE_LOOPBACK then
          Exit;
      end;
      Adapter := Adapter^.Next;
    end;
  finally
    FreeMem(Buffer);
  end;
end;

function ReadRegistryMachineGuid: string;
var
  Reg: TRegistry;
begin
  Result := '';
  Reg := TRegistry.Create(KEY_READ);
  try
    Reg.RootKey := HKEY_LOCAL_MACHINE;
    if Reg.OpenKeyReadOnly('SOFTWARE\Microsoft\Cryptography') then
      Result := Trim(Reg.ReadString('MachineGuid'));
  finally
    Reg.Free;
  end;
end;

function ReadSystemVolumeSerial: string;
var
  Serial: DWORD;
  MaxComponent: DWORD;
  FileSystemFlags: DWORD;
  Drive: string;
begin
  Result := '';
  Serial := 0;
  MaxComponent := 0;
  FileSystemFlags := 0;
  Drive := 'C:\';
  if _GetVolumeInformationW(PWideChar(Drive), nil, 0, @Serial, @MaxComponent, @FileSystemFlags, nil, 0) then
    Result := IntToHex(Serial, 8);
end;

function HashToGuidPattern(const HexValue: string): string;
var
  Clean: string;
begin
  Clean := StringReplace(HexValue, '-', '', [rfReplaceAll]);
  while Length(Clean) < 32 do
    Clean := Clean + '0';
  Result := Format('{%s-%s-%s-%s-%s}',
    [Copy(Clean, 1, 8), Copy(Clean, 9, 4), Copy(Clean, 13, 4),
     Copy(Clean, 17, 4), Copy(Clean, 21, 12)]);
end;

function BuildInstallationId(const ComputerName: string): string;
var
  Source: string;
  Hash: string;
begin
  Source := ReadRegistryMachineGuid + '|' + ReadSystemVolumeSerial + '|' + UpperCase(Trim(ComputerName));
  if (Pos('|', Source) > 0) and (StringReplace(Source, '|', '', [rfReplaceAll]) <> '') then
  begin
    Hash := THashSHA2.GetHashString(Source);
    Result := HashToGuidPattern(Hash);
  end
  else
    Result := '';
end;

end.
